namespace GeneSort.Project

open System
open System.Threading

open FSharp.UMX

open GeneSort.Core
open GeneSort.Sorter
open GeneSort.Model.Sortable
open GeneSort.SortingOps
open GeneSort.Runs
open GeneSort.Db


module FullBoolEvals =

    let fullBoolEvalsProjectName = "FullBoolEvals" |> UMX.tag<projectName>
    let projectDesc = "FullBoolEvals on RandomSorters with SortingWidth from 4 to 16 generated by Msce, Mssi, Msrs, and Msuf"
    let randomSorters4to64ProjectName = "RandomSorters4to64" |> UMX.tag<projectName>
    let randomType = rngType.Lcg
    let sortableDataType = sortableDataType.Bools

    let makeQueryParamsForFullBoolEvals 
            (repl: int<replNumber> option) 
            (sortingWidth:int<sortingWidth> option)
            (sorterModelType:sorterModelType option)
            (outputDataType: outputDataType) =
        queryParams.create(
            Some fullBoolEvalsProjectName,
            repl,
            outputDataType,
            [|
                (runParameters.sortingWidthKey, sortingWidth |> SortingWidth.toString); 
                (runParameters.sorterModelTypeKey, sorterModelType |> SorterModelType.toString);
            |])


    let makeQueryParamsForFullBoolEvalsFromRunParams
            (runParams: runParameters) 
            (outputDataType: outputDataType) =
        makeQueryParamsForFullBoolEvals
            (runParams.GetRepl())
            (runParams.GetSortingWidth())
            (runParams.GetSorterModelType())
            outputDataType


    let makeQueryParamsForRandomSorters4to64ProjectName
            (repl: int<replNumber> option) 
            (sortingWidth:int<sortingWidth> option)
            (sorterModelType:sorterModelType option)
            (outputDataType: outputDataType) =
        queryParams.create(
            Some randomSorters4to64ProjectName,
            repl,
            outputDataType,
            [|
                (runParameters.sortingWidthKey, sortingWidth |> SortingWidth.toString); 
                (runParameters.sorterModelTypeKey, sorterModelType |> SorterModelType.toString);
            |])


    let makeQueryParamsForRandomSorters4to64FromRunParams
            (runParams: runParameters) 
            (outputDataType: outputDataType) =
        makeQueryParamsForRandomSorters4to64ProjectName
            (runParams.GetRepl())
            (runParams.GetSortingWidth())
            (runParams.GetSorterModelType())
            outputDataType


    let sortingWidthValues = 
        [4; 6; 8; 12; ] |> List.map(fun d -> d.ToString())

    let sortingWidths() : string*string list =
        (runParameters.sortingWidthKey, sortingWidthValues)
        
    let sorterModelKeyValues() : string list =
        [ Some sorterModelType.Mcse; 
          Some sorterModelType.Mssi;
          Some sorterModelType.Msrs; 
          Some sorterModelType.Msuf4; 
          Some sorterModelType.Msuf6; ]  |> List.map(SorterModelType.toString)

    let sorterModelKeys() : string*string list =
        (runParameters.sorterModelTypeKey, sorterModelKeyValues() )



    let sorterModelTypeForSortingWidth (rp: runParameters) =
        let sorterModelKey = rp.GetSorterModelType().Value
        let sortingWidth = rp.GetSortingWidth().Value
        let has2factor = (%sortingWidth % 2 = 0)
        let isMuf4able = (MathUtils.isAPowerOfTwo %sortingWidth)
        let isMuf6able = (%sortingWidth % 3 = 0) && (MathUtils.isAPowerOfTwo (%sortingWidth / 3))

        match sorterModelKey with
        | sorterModelType.Mcse -> Some rp
        | sorterModelType.Mssi
        | sorterModelType.Msrs -> if has2factor then Some rp else None
        | sorterModelType.Msuf4 ->
                if isMuf4able then Some rp else None
        | sorterModelType.Msuf6 -> 
                if isMuf6able then Some rp else None
                


    let paramMapFilter (rp: runParameters) = 
        Some rp
        |> Option.bind sorterModelTypeForSortingWidth


    let paramMapRefiner (runParametersSeq: runParameters seq) : runParameters seq = 

        let enhancer (rp : runParameters) : runParameters =
            let qp = makeQueryParamsForFullBoolEvalsFromRunParams rp (outputDataType.RunParameters)

            rp.WithProjectName(fullBoolEvalsProjectName)
              .WithRunFinished(false)
              .WithSortableDataType(Some sortableDataType)
              .WithId (qp.Id.ToString() |> UMX.tag<idValue>)



        seq {
            for runParameters in runParametersSeq do
                    let filtrate = paramMapFilter runParameters
                    if filtrate.IsSome then
                        let retVal = filtrate.Value |> enhancer
                        yield retVal
        }


    let parameterSpans = 
        [ sortingWidths(); sorterModelKeys();]

    let outputDataTypes = 
            [|
                outputDataType.RunParameters;
                outputDataType.SorterSetEval "";
                outputDataType.TextReport ("Bins" |> UMX.tag<textReportName>); 
                outputDataType.TextReport ("Profiles" |> UMX.tag<textReportName>); 
                outputDataType.TextReport ("Report3" |> UMX.tag<textReportName>); 
                outputDataType.TextReport ("Report4" |> UMX.tag<textReportName>);
            |]


    let project = 
            project.create 
                fullBoolEvalsProjectName 
                projectDesc
                parameterSpans
                outputDataTypes


    let executor
            (db: IGeneSortDb)
            (runParameters: runParameters) 
            (allowOverwrite: bool<allowOverwrite>)
            (cts: CancellationTokenSource) 
            (progress: IProgress<string> option) : Async<runParameters> =

        async {
            let runId = runParameters.GetId() |> Option.defaultValue (% "unknown")
            let repl = runParameters.GetRepl() |> Option.defaultValue (0 |> UMX.tag)
            let sortingWidth = runParameters.GetSortingWidth().Value
            let sortableDataType = runParameters.GetSortableDataType().Value

            // Check cancellation before starting expensive operations
            cts.Token.ThrowIfCancellationRequested()
            progress |> Option.iter (fun p -> 
                p.Report(sprintf "Run: %s Repl: %d: Querying sortable set" %runId %repl ))



            // FIX: Use let! instead of Async.RunSynchronously to keep the workflow non-blocking
            let queryParamsForSorterSet = makeQueryParamsForRandomSorters4to64FromRunParams runParameters (outputDataType.SorterSet "")
            let! loadResult = db.loadAsync queryParamsForSorterSet
            let sorterSet = 
                match loadResult with
                | Ok (outputData.SorterSet ss) -> ss
                | Ok _ -> failwith "Unexpected output data type: expected SorterSet"
                | Error err -> failwithf "Error loading SorterSet for run %s: %s" %runId err


            let sortableTestModel = msasF.create sortingWidth |> sortableTestModel.MsasF
            let sortableTests = SortableTestModel.makeSortableTests sortableTestModel sortableDataType
            let sorterSetEval = SorterSetEval.makeSorterSetEval sorterSet sortableTests

            cts.Token.ThrowIfCancellationRequested()
            progress |> Option.iter (fun p -> p.Report(sprintf "Run %s_%d: Saving sorterSet test results" %runId %repl))

            
            // Save sorterSetEval
            let queryParamsForSorterSetEval = makeQueryParamsForFullBoolEvalsFromRunParams runParameters (outputDataType.SorterSetEval "")
            let! saveResult = db.saveAsync queryParamsForSorterSetEval (sorterSetEval |> outputData.SorterSetEval) allowOverwrite
            match saveResult with
            | Ok _ ->
                progress |> Option.iter (fun p -> p.Report(sprintf "Run %s finished successfully." %runId))
                // Return the "Finished" version of the parameters
                return runParameters.WithRunFinished true
            | Error err ->
                progress |> Option.iter (fun p -> p.Report(sprintf "Failed to save run %s: %s" %runId err))
                // Return original state on failure (or you could add an Error status key)
                return runParameters 

    }



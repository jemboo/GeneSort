namespace GeneSort.SortingOps

open System.Runtime.Intrinsics
open System.Threading.Tasks
open FSharp.UMX
open GeneSort.Sorting
open GeneSort.Sorting.Sortable
open System.Collections.Concurrent
open System.Collections.Generic
open System
open System.Threading

module CeBlockOpsUint8v512 =

    /// Check if all 64 lanes in the block are sorted.
    let IsBlockSorted (len: int) (vectors: Vector512<uint8>[]) =
        let mutable sorted = true
        let mutable i = 0
        while i < len - 1 && sorted do
            let vLow = vectors.[i]
            let vHi = vectors.[i+1]
            if Vector512.Min(vLow, vHi) <> vLow then
                sorted <- false
            else
                i <- i + 1
        sorted

    /// Mask-optimized: Check if all 64 lanes in the block are sorted.
    let IsBlockSortedMask (len: int) (vectors: Vector512<uint8>[]) =
        let mutable sorted = true
        let mutable i = 0
        while i < len - 1 do
            let vLow = vectors.[i]
            let vHi = vectors.[i+1]
            
            // Mask: 0xFF where ordered (Low <= Hi), 0x00 otherwise
            let mask = Vector512.Equals(Vector512.Min(vLow, vHi), vLow)
            
            // AVX-512 ExtractMostSignificantBits for uint8 returns uint64 (64 bits)
            let bitmask = Vector512.ExtractMostSignificantBits(mask)
            
            // If all 64 lanes are sorted, bitmask will be 0xFFFFFFFFFFFFFFFF
            if bitmask <> UInt64.MaxValue then
                sorted <- false
                i <- len // Break
            else
                i <- i + 1
        sorted

    /// Computes 64 separate hashes for the 64 lanes in the block.
    let computeLaneHashes64 (len: int) (vectors: Vector512<uint8>[]) =
        let hashes = Array.create 64 17u
        for i = 0 to len - 1 do
            let v = vectors.[i]
            for lane = 0 to 63 do
                hashes.[lane] <- hashes.[lane] * 31u + uint32 (v.GetElement(lane))
        hashes

    let inline hashLane (block: Vector512<uint8>[]) (lane: int) =
        let mutable h = 17
        for i = 0 to block.Length - 1 do
            h <- h * 31 + int (block.[i].GetElement(lane))
        h

    type ArrayContentComparer<'T when 'T : equality>() =
        interface IEqualityComparer<'T[]> with
            member _.Equals(x, y) =
                if obj.ReferenceEquals(x, y) then true
                elif isNull x || isNull y then false
                elif x.Length <> y.Length then false
                else ReadOnlySpan<'T>(x).SequenceEqual(ReadOnlySpan<'T>(y))

            member _.GetHashCode(obj) =
                let mutable h = 17
                for i = 0 to obj.Length - 1 do
                    h <- h * 31 + hash (obj.[i])
                h

    let evalSimdSortBlocks
        (simdSortBlocks: SortBlockUint8v512 seq) 
        (ceBlocks: ceBlock array) 
        : ceBlockEval [] =
    
        let numNetworks = ceBlocks.Length
        if numNetworks = 0 then [||] else

        let maxWidth = ceBlocks |> Array.maxBy (fun c -> %c.SortingWidth) |> (fun c -> %c.SortingWidth)
        let networkData = ceBlocks |> Array.map (fun ceb -> 
            {| Lows = ceb.CeArray |> Array.map (fun c -> c.Low)
               Highs = ceb.CeArray |> Array.map (fun c -> c.Hi)
               CeLen = ceb.CeArray.Length |})

        let globalUsage = Array.init numNetworks (fun i -> ceUseCounts.Create(ceBlocks.[i].CeLength))
        let globalUnsorted = Array.zeroCreate<int> numNetworks

        Parallel.ForEach(
            simdSortBlocks, 
            (fun () -> 
                let usage = Array.init numNetworks (fun i -> Array.zeroCreate<int> networkData.[i].CeLen)
                let unsorted = Array.zeroCreate<int> numNetworks
                let buffer = Array.zeroCreate<Vector512<uint8>> maxWidth
                (usage, unsorted, buffer)),
    
            (fun currentBlock loopState ((localUsage : int [][]), (localUnsorted: int[]), workBuffer) ->
                let currentLen = currentBlock.Length

                for nIdx = 0 to numNetworks - 1 do
                    Array.blit currentBlock.Vectors 0 workBuffer 0 currentLen
                    let data = networkData.[nIdx]
                    let ceCount = data.CeLen
                
                    for cIdx = 0 to ceCount - 1 do
                        let lIdx = data.Lows.[cIdx]
                        let hIdx = data.Highs.[cIdx]
                        let vLow = workBuffer.[lIdx]
                        let vHi = workBuffer.[hIdx]
                        let vMin = Vector512.Min(vLow, vHi)
                
                        if vLow <> vMin then
                            localUsage.[nIdx].[cIdx] <- localUsage.[nIdx].[cIdx] + 1
                            workBuffer.[lIdx] <- vMin
                            workBuffer.[hIdx] <- Vector512.Max(vLow, vHi)

                    if not (IsBlockSortedMask currentLen workBuffer) then
                        localUnsorted.[nIdx] <- localUnsorted.[nIdx] + 1
            
                (localUsage, localUnsorted, workBuffer)),

            (fun ((localUsage: int [][]), (localUnsorted: int[]), _) ->
                for nIdx = 0 to numNetworks - 1 do
                    Interlocked.Add(&globalUnsorted.[nIdx], localUnsorted.[nIdx]) |> ignore
                    let globalArr = globalUsage.[nIdx]
                    for i = 0 to localUsage.[nIdx].Length - 1 do
                        globalArr.IncrementAtomicBy (i |> UMX.tag) localUsage.[nIdx].[i]
            )
        ) |> ignore

        Array.init numNetworks (fun i ->
            ceBlockEval.create ceBlocks.[i] globalUsage.[i] (globalUnsorted.[i] |> UMX.tag) None
        )

    let evalAndCollectUniqueFailures
        (simdSortBlocks: SortBlockUint8v512 seq) 
        (ceBlocks: ceBlock array) 
        : ceBlockEval [] =
    
        let numNetworks = ceBlocks.Length
        if numNetworks = 0 then [||] else

        let maxWidth = ceBlocks |> Array.maxBy (fun c -> %c.SortingWidth) |> (fun c -> %c.SortingWidth)
        let networkData = ceBlocks |> Array.map (fun ceb -> 
            {| Lows = ceb.CeArray |> Array.map (fun c -> c.Low)
               Highs = ceb.CeArray |> Array.map (fun c -> c.Hi)
               SortingWidth = ceb.SortingWidth
               CeLen = ceb.CeArray.Length |})

        let globalUsage = Array.init numNetworks (fun i -> ceUseCounts.Create(ceBlocks.[i].CeLength))
        let globalUnsorted = Array.zeroCreate<int> numNetworks
        let failureSets = Array.init numNetworks (fun _ -> ConcurrentDictionary<int[], byte>(ArrayContentComparer<int>()))

        Parallel.ForEach(
            simdSortBlocks, 
            (fun () -> 
                let usage = Array.init numNetworks (fun i -> Array.zeroCreate<int> networkData.[i].CeLen)
                let unsorted = Array.zeroCreate<int> numNetworks
                let buffer = Array.zeroCreate<Vector512<uint8>> maxWidth
                (usage, unsorted, buffer)),
    
            (fun currentBlock loopState ((localUsage: int[][]), (localUnsorted: int[]), workBuffer) ->
                let currentLen = currentBlock.Length

                for nIdx = 0 to numNetworks - 1 do
                    Array.blit currentBlock.Vectors 0 workBuffer 0 currentLen
                    let data = networkData.[nIdx]
                    let ceCount = data.CeLen
                    let goldenHashes = Simd512GoldenHashProvider.GetGoldenHashes data.SortingWidth
            
                    for cIdx = 0 to ceCount - 1 do
                        let lIdx = data.Lows.[cIdx]
                        let hIdx = data.Highs.[cIdx]
                        let vLow = workBuffer.[lIdx]
                        let vHi = workBuffer.[hIdx]
                        let vMin = Vector512.Min(vLow, vHi)
                
                        if vLow <> vMin then
                            localUsage.[nIdx].[cIdx] <- localUsage.[nIdx].[cIdx] + 1
                            workBuffer.[lIdx] <- vMin
                            workBuffer.[hIdx] <- Vector512.Max(vLow, vHi)

                    if not (IsBlockSortedMask currentLen workBuffer) then
                        localUnsorted.[nIdx] <- localUnsorted.[nIdx] + 1
                        let currentHashes = computeLaneHashes64 currentLen workBuffer
                    
                        for lane = 0 to currentBlock.SortableCount - 1 do
                            if currentHashes.[lane] <> goldenHashes.[lane] then
                                let laneArray = Array.init currentLen (fun i -> int (workBuffer.[i].GetElement(lane)))
                                failureSets.[nIdx].TryAdd(laneArray, 0uy) |> ignore
            
                (localUsage, localUnsorted, workBuffer)),

            (fun ((localUsage: int[][]), (localUnsorted: int[]), _) ->
                for nIdx = 0 to numNetworks - 1 do
                    Interlocked.Add(&globalUnsorted.[nIdx], localUnsorted.[nIdx]) |> ignore
                    let globalArr = globalUsage.[nIdx]
                    for i = 0 to localUsage.[nIdx].Length - 1 do
                        globalArr.IncrementAtomicBy (i |> UMX.tag) localUsage.[nIdx].[i]
            )
        ) |> ignore

        Array.init numNetworks (fun i ->
            let uniqueFailures = failureSets.[i].Keys |> Seq.toArray
            let failTest = 
                if uniqueFailures.Length > 0 then
                    let sw = ceBlocks.[i].SortingWidth
                    let sss = int sw |> UMX.tag<symbolSetSize>
                    uniqueFailures 
                    |> Array.map (fun arr -> sortableIntArray.create(arr, sw, sss))
                    |> sortableIntTest.create (Guid.NewGuid() |> UMX.tag) sw
                    |> sortableTest.Ints
                    |> Some
                else None

            ceBlockEval.create 
                ceBlocks.[i] 
                globalUsage.[i] 
                (globalUnsorted.[i] |> UMX.tag<sortableCount>) 
                failTest
        )

    let eval (test: sortableUint8v512Test) (ceBlocks: ceBlock []) =
        evalSimdSortBlocks test.SimdSortBlocks ceBlocks

    let evalAndCollectNewSortableTests (test: sortableUint8v512Test) (ceBlocks: ceBlock []) =
        evalAndCollectUniqueFailures test.SimdSortBlocks ceBlocks